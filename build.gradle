// Plugins
plugins {
    id 'java'
    id 'java-library-distribution'
    id 'org.sonarqube' version '2.6.2'
    id 'jacoco'
    id 'com.zoltu.git-versioning' version '3.0.3'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
}

// Java
group 'com.ramirezblauvelt.democi'
description = 'Aplicación para sumar días hábiles a fechas de referencia'
sourceCompatibility = 1.8
targetCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// Ejecutar y depurar sin 'application' plugin en Netbeans
if (!hasProperty('mainClass')) {
    ext.mainClass = 'com.ramirezblauvelt.democi.Fechas'
}

// Maven
repositories {
    mavenCentral()
}

// AWS BOM
dependencyManagement {
    imports {
        mavenBom 'com.amazonaws:aws-java-sdk-bom:1.11.347'
    }
}

// Dependencias
dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1'
    compile group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.27'
    compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.27'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'

    compile group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.0'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '2.1.0'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-log4j2', version: '1.1.0'

    compile group: 'com.amazonaws', name: 'aws-java-sdk-lambda'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Java Library Distribution
tasks.distTar.enabled = false
tasks.distZip.enabled = false

// JAR
jar {
    baseName = 'DemoCI'
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'
    manifest {
        attributes \
			'Main-Class': 'com.ramirezblauvelt.democi.Fechas',
                'Class-Path': configurations.runtime.files.collect { "lib/$it.name" }.join(' ')
    }
}

// Sonar
sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.login", "2e7818d4c50749bfcef2b46c50a479bbb0fc25a3"
        property "sonar.organization", "sophos-rjrb-bitbucket"
        property "sonar.scm.disabled", "True"
    }
}

// Paquete para AWS Lambda
task buildZipAWS(type: Zip) {
    archiveName = 'DemoCI.zip'
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtime
    }
}
build.dependsOn buildZipAWS

// Muestra la versión que se generará (ZoltuGitVersioning)
def versionInfo = ZoltuGitVersioning.versionInfo
print "${versionInfo.major}.${versionInfo.minor}.${versionInfo.commitCount}" + System.getProperty("line.separator")
